plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.10.0'
}

group = "com.limark.open.gradle.plugins"

/**********************************************************************************************************************
 * Copyright (c) 2018 Limark Technologies (Pvt) Ltd.                                                                  *
 *                                                                                                                    *
 *  Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated      *
 *  documentation files (the "Software"), to deal in the Software without restriction, including without limitation   *
 *  the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and  *
 *  to permit persons to whom the Software is furnished to do so, subject to the following conditions:                *
 *                                                                                                                    *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of   *
 * the Software.                                                                                                      *
 *                                                                                                                    *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO   *
 * THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL         *
 * THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF      *
 * CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE                               *
 **********************************************************************************************************************/

// Dog Fooding - We use the plugin to manage itself
apply plugin: new GroovyScriptEngine(
    [
        file('src/main/groovy').absolutePath, 
        file('src/main/resources').absolutePath
    ].toArray(new String[2]),
    this.class.classLoader
).loadScriptByName('com/limark/open/gradle/plugins/gitflowsemver/GitFlowSemVerPlugin.groovy')

dependencies {
  implementation gradleApi()
  implementation localGroovy()
}

jar {
  baseName = "gitflow-semver"
}

pluginBundle {
  website = 'https://github.com/OpenLimark/GitFlowSemVerPlugin'
  vcsUrl = 'https://github.com/OpenLimark/GitFlowSemVerPlugin'
  tags = ['semver', 'gitflow','limark']

  plugins {
    gitFlowSemverPlugin {
      id = 'com.limark.gitflowsemver'
      displayName = 'GitFlow SemVer 2.0 Plugin'
      description = "An opionionated GitFlow SemVer 2.0 versioning plugin that resolves project vesion based on Git Tags and conventions."
      tags = ['semver', 'gitflow', 'limark']
    }
  }
}