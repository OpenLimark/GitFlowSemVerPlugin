plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.10.0'
}

group = "com.limark.open.gradle.plugins"

// Dog Fooding - We use the plugin to manage itself
apply plugin: new GroovyScriptEngine(
    [
        file('src/main/groovy').absolutePath, 
        file('src/main/resources').absolutePath
    ].toArray(new String[2]),
    this.class.classLoader
).loadScriptByName('com/limark/open/gradle/plugins/gitflowsemver/GitFlowSemVerPlugin.groovy')

dependencies {
  implementation gradleApi()
  implementation localGroovy()
}

jar {
  baseName = "gitflow-semver"
}

pluginBundle {
  website = 'https://github.com/OpenLimark/GitFlowSemVerPlugin'
  vcsUrl = 'https://github.com/OpenLimark/GitFlowSemVerPlugin'
  tags = ['semver', 'gitflow','limark']

  plugins {
    gitFlowSemverPlugin {
      id = 'com.limark.gitflowsemver'
      displayName = 'GitFlow SemVer 2.0 Plugin'
      description = "An opionionated GitFlow SemVer 2.0 versioning plugin that resolves project vesion based on Git Tags and conventions."
      tags = ['semver', 'gitflow', 'limark']
    }
  }
}